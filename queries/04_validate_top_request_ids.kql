let appName = "{{APP_NAME}}";
let window = 24h;
let validate_route = "/api/codes/candidates/{id}/validate";

union isfuzzy=true ContainerAppConsoleLogs, ContainerAppConsoleLogs_CL
| where TimeGenerated > ago(window)
| extend AppName = tostring(coalesce(ContainerAppName, ContainerAppName_s))
| where AppName == appName
| extend RawLog = tostring(coalesce(Log, Log_s))
| extend j = parse_json(RawLog)
| where tostring(j.event) in ("request.end", "request.slow")
| extend
    route = tostring(j.route),
    req_id = tostring(j.request_id),
    dur = todouble(j.duration_ms),
    status = toint(j.status_code),
    build = tostring(j.build_version),
    test_run = tostring(j.test_run_id)
| where route == validate_route
| where isnotempty(req_id) and isnotnull(dur)
| top 20 by dur desc
| project TimeGenerated, dur, status, req_id, build, test_run
